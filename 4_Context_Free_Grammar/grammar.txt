DOCUMENTATION:
Non-terminals: Enclosed in < >
Terminals: strings in quotes eg:"int"
Peoductions: Non-terminal followed by "->" and a sequence of terminals and non-terminals eg:<program> -> "int" <decllist> ";" <cmpdstmt> "."
Start symbol: <program>


Non-terminal symbols:
<program>, <declarationList>, <declaration>, <varDeclaration>, <arrayDeclaration>, <structDeclaration>, <array>,
<type>, <struct>, <instructionSet>, <instruction>, <simpleInstruction>, <structuredInstruction>, <assignment>,
<structure>, <expression>, <term>, <read>, <print>, <if>, <while>, <for>, <start>, <end>, <step>, <condition>,
<arithmeticOperators>, <comparisonOperators>, <identifier>, <element>, <elements>, <number>, <integerNumber>,
<realNumber>, <sign>, <digits>, <string>, <character>, <characters>, <letter>, <uppercaseLetter>, <lowercaseLetter>,
<digit>, <nonZeroDigit>, <symbol>, <separators>

Terminals:
"Algorithm", ":", "EndAlgorithm", ";", "int", "char", "string", "bool", "read", "print", "if", "then", "else",
"while", "execute", "for", "=", "+", "-", "*", "/", "%", "==", "<", "<=", ">", ">=", "!", "&&", "||", ".", "[", "]",
"\"", " ", "{", "}", "_", "?", "#", "0", "1", ..., "9", "A", "B", ..., "Z", "a", "b", ..., "z"


PRODUCTIONS:
<program> -> "Algorithm" ":" <declarationList> <instructionSet> "EndAlgorithm"

<declarationList> -> <declaration> | <declaration> ";" <declarationList>
<declaration> -> <varDeclaration> | <arrayDeclaration> | <structDeclaration>
<varDeclaration> -> <identifier> ":" <type>
<arrayDeclaration> -> <array> "[" <integerNumber> "]" ":" "array" <type>
<array> -> <identifier> "[" <expression> "]"
<type> -> "int" | "char" | "string" | "bool"
<structDeclaration> -> <identifier> ":" <uppercaseLetter> <identifier>
<struct> -> <uppercaseLetter> <identifier> ":" "struct" "{" <declarationList> "}"

<instructionSet> -> <instruction> | <instruction> ";" <instructionSet>
<instruction> -> <simpleInstruction> | <structuredInstruction>

<simpleInstruction> -> <assignment> | <read> | <print>
<assignment> -> <structure> "=" <expression>
<structure> -> <identifier> | <array>
<expression> -> <term> | <term> <arithmeticOperators> <term>
<term> -> <number> | <structure>

<read> -> "read" <identifier> | "read" <array>
<print> -> "print" <structure>

<structuredInstruction> -> <if> | <while> | <for>
<if> -> "if" <condition> "then" <instruction> | "if" <condition> "then" <instruction> "else" <instruction>
<while> -> "while" <condition> "execute" <instruction>
<for> -> "for" <start> "" <end> "" <step> "execute" <instruction>

<start> -> <identifier> "=" <expression>
<end> -> <expression>
<step> -> <integerNumber>

<condition> -> <expression> <comparisonOperators> <expression> | "!" <condition> | <condition> "&&" <condition> | <condition> "||" <condition>

<arithmeticOperators> -> "=" | "+" | "-" | "*" | "/" | "%"
<comparisonOperators> -> "==" | "<" | "<=" | ">" | ">="

<identifier> -> <letter> | <letter> <elements> | <identifier> "." <identifier>
<element> -> <letter> | <digit>
<elements> -> <element> | <element> <elements>

<number> -> <integerNumber> | <realNumber>
<integerNumber> -> <sign> <nonZeroDigit> <digits> | <sign> <nonZeroDigit> | <nonZeroDigit> <digits>
<sign> -> "+" | "-"
<digits> -> <digit> | <digit> <digits>
<realNumber> -> <integerNumber> "." <digits> <nonZeroDigit> | <digits> "." <digits> <nonZeroDigit> | <integerNumber> "." <nonZeroDigit> | <digits> "." <nonZeroDigit>
<string> -> '"' <character> '"' | '"' <character> <characters> '"'
<character> -> <letter> | <digit> | <separators> | <symbol>
<characters> -> <character> | <character> <characters>

<letter> -> <uppercaseLetter> | <lowercaseLetter>
<uppercaseLetter> -> [A-Z]
<lowercaseLetter> -> [a-z]
<digit> -> [0-9]
<nonZeroDigit> -> [1-9]
<symbol> -> "_" | "." | "!" | "?" | "#"

<separators> -> "[" | "]" | "\"" | ":" | ";" | " " | "{" | "}"
